apply from: "${rootDir}/gradle/java-project.gradle"
apply from: "${rootDir}/gradle/java-codequality.gradle"
apply from: "${rootDir}/gradle/guice-application.gradle"
apply from: "${rootDir}/gradle/license-headers.gradle"
apply from: "${rootDir}/gradle/publishing-java.gradle"
apply plugin: 'org.kordamp.gradle.stats'

if (!hasProperty('mainClass')) {
  ext.mainClass = 'org.opentcs.operationsdesk.RunOperationsDesk'
}
mainClassName = ext.mainClass

ext.collectableDistDir = new File(buildDir, 'install')

dependencies {
  api project(':openTCS-Common')
  api project(':openTCS-Impl-Configuration-cfg4j')
  api project(':openTCS-PlantOverview-Common')
  api project(':openTCS-PlantOverview-Panel-LoadGenerator')
  api project(':openTCS-PlantOverview-Panel-ResourceAllocation')
  api project(':openTCS-PlantOverview-Themes-Default')

  runtimeOnly group: 'org.slf4j', name: 'slf4j-jdk14', version: '1.7.36'
}

compileJava {
  options.compilerArgs << "-Xlint:-rawtypes"
}

distributions {
  main {
    contents {
      from "${sourceSets.main.resources.srcDirs[0]}/org/opentcs/operationsdesk/distribution"
    }
  }
}

// For now, we're using hand-crafted start scripts, so disable the application
// plugin's start script generation.
startScripts.enabled = false

task release {
  dependsOn build
  dependsOn installDist
}

run {
  systemProperties(['java.util.logging.config.file':'./config/logging.config',\
                    'java.security.policy':'file:./config/java.policy',\
                    'sun.java2d.d3d':'false',\
                    'opentcs.base':'.',\
                    'opentcs.home':'.',\
                    'opentcs.cfg4j.reload.interval':'10000'])
  jvmArgs('-XX:-OmitStackTraceInFastThrow',\
          '-splash:bin/splash-image.gif')
}
